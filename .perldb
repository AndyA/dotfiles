use TextMate::JumpTo qw(jumpto);
use File::Spec;

my @opts = qw( windowSize=30 );

if ( $] >= 5.010000 ) {
    push @opts, "HistFile='" . glob( '~/.perldb_history' ) . "'";
}

parse_options( $_ ) for @opts;

sub afterinit {
    $trace |= 4;    # Enable watchfunction

    # Needed to work out where filenames are relative to
    chomp( $base_dir = `pwd` );

    $option{animate} = 0;
    push @options, 'animate';
}

sub watchfunction {
    my ( $package, $file, $line ) = @_;

    return unless $single || $signal || $option{animate};
    return if $file =~ m{/perl5db[.]pl$};

    local $trace = 0;

    if ( $file =~ /^\(eval\s+\d+\)\[(.+?):(\d+)\]/ ) {
        $file = $1;
        $line += $2 - 1;
    }

    jumpto(
        file => File::Spec->rel2abs( $file, $base_dir ),
        line => $line,
        bg   => 1
    );

    return 1;
}
