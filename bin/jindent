#!/usr/bin/env perl

use latest;

use Fcntl;
use File::Spec;
use File::Temp qw( :seekable );
use File::Which;
use Getopt::Long;

GetOptions( 'config:s' => \my $config ) or die "Stopping\n";

defined $config
 or die "Please specify the config file using the --config switch";

( $config, @ARGV )
 = map { File::Spec->rel2abs( $_ ) } ( $config, @ARGV );

if ( @ARGV ) {
  tidy( $config, @ARGV );
}
else {
  my $fh = File::Temp->new( SUFFIX => '.java' );
  print $fh <STDIN>;
  tidy( $config, $fh->filename );
  $fh->seek( 0, SEEK_SET );
  print <$fh>;
}

sub tidy {
  my ( $config, @files ) = @_;
  my $eclipse = $ENV{ECLIPSE} // which( 'eclipse' )
   // die
   "Can't find eclipse - please set the ECLIPSE environment variable\n";

  my $cmd = join ' ', $eclipse, '-nosplash', '-application',
   'org.eclipse.jdt.core.JavaCodeFormatter', '-config', $config, @files,
   '> /dev/null';

  system $cmd and die "$cmd failed: $?\n";
}
