#!/usr/bin/env perl

use 5.010;

use autodie;
use strict;
use warnings;

use File::Find;

for my $dir (@ARGV) {
  my @ts = stable( find_ts($dir) );
  for my $ts (@ts) {
    ( my $mp4 = $ts ) =~ s/\.ts$/.mp4/i;
    ( my $tmp = $ts ) =~ s/\.ts$/.tmp.mp4/i;
    if ( -f $mp4 && -M $mp4 < -M $ts ) {
      print "$mp4 exists, skipping\n";
      next;
    }
    my $map = probe($ts);

    my @cmd = (
      ffmpeg => -y => -i => $ts,
      ( map { ( -map => $_ ) } map { $_->[0] } $map->{video}{unknown},
        $map->{audio}{eng}
      ),
      '-c:a' => 'libfaac',
      '-b:a' => '256k',
      '-c:v' => 'copy',
      $tmp
    );
    print join( ' ', @cmd ), "\n";
    my $rc = system @cmd;
    print "ffmpeg exit: $rc\n";
    if ($rc) {
      unlink $tmp;
      next;
    }
    rename $tmp, $mp4;
  }
}

sub stable {
  my @name = @_;
  my @state = map { [$_, ( stat $_ )[9]] } @name;
  sleep 5;
  @state = map { [@$_, ( stat $_->[0] )[9]] } @state;
  return map { $_->[0] } grep { $_->[1] == $_->[2] } @state;
}

sub probe {
  my $vf  = shift;
  my $tmp = "/tmp/ffprobe.$$.log";
  my $cmd = join ' ', 'ffprobe',
   -i => shell_escape($vf),
   '>', shell_escape($tmp), '2>&1';
  print "$cmd\n";
  my $rc = system $cmd;
  die "ffprobe failed: $rc" if $rc;
  my $map = {};
  open my $tf, '<', $tmp;

  while (<$tf>) {
    chomp;
    if (/^\s*Stream\s+\#(\d+:\d+)\[0x[0-9a-f]+\](?:\(([^\)]+)\))?:\s+(\w+):/)
    {
      my ( $id, $lang, $type ) = ( $1, $2, $3 );
      $lang //= 'unknown';
      push @{ $map->{ lc $type }{ lc $lang } }, $1;
    }
  }
  return $map;
}

sub shell_escape {
  ( my $word = shift ) =~ s/([!\s"'*?:(){};\$<>&\\])/\\$1/g;
  return $word;
}

sub find_ts {
  my $dir = shift;
  return ($dir) if -f $dir;
  my @ts = ();
  find {
    wanted => sub {
      return unless -f && /\.ts$/i;
      push @ts, $_;
    },
    no_chdir => 1,
  }, $dir;
  return sort @ts;
}

# vim:ts=2:sw=2:sts=2:et:ft=perl

