#!/usr/bin/env perl

use strict;
use warnings;

use constant HOMEHOSTS => '~/.ripley';

my @hosts = gethosts();

my %by_dir = ();
my $home   = glob '~';

for my $obj ( @ARGV ) {
  $obj =~ s{/$}{} if -d $obj;
  $obj =~ s{^\Q$home}{~};
  my ( $dir, $leaf )
   = ( $obj =~ m{(.+)/([^/]+)$} )
   ? ( $1, $2 )
   : ( '.', $obj );
  $by_dir{$dir}{$leaf}++;
}

for my $host ( @hosts ) {
  for my $dir ( sort keys %by_dir ) {
    my @obj = map { "$dir/$_" } sort keys %{ $by_dir{$dir} };
    my $cmd = join ' ', 'rsync', '-az', @obj, "$host:$dir";
    print "  $cmd\n";
    system $cmd;
  }
}

sub gethosts {
  open my $hh, '<', glob HOMEHOSTS
   or die "Can't read ", HOMEHOSTS, ": $!\n";
  my $hosts = do { local $/; <$hh> };
  die "Huh? ($hosts)\n" unless $hosts =~ /^HOSTS="([^"]*)"/;
  return split /\s+/, $1;
}

# vim:ts=2:sw=2:sts=2:et:ft=perl

