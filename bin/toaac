#!/usr/bin/env perl

use 5.010;

use strict;
use warnings;

use File::Find;

use constant (
  SAMPLE_RATE => 44100,
  BIT_RATE    => '256k',
  EXTENSION   => 'm4a',
);

@ARGV = '.' unless @ARGV;

find {
  no_chdir => 1,
  wanted   => sub {
    return unless /\.(?:flac|wav|wma)$/i;
    toaac( $_ );
  },
 },
 @ARGV;

sub toaac {
  my $raw = shift;
  my $ext = EXTENSION;
  ( my $m4a = $raw ) =~ s/\.(\w+)$/.$ext/;
  if ( -f $m4a ) {
    say "Skipping $raw, $ext file already exists";
    return;
  }
  my $tmp = "$m4a.tmp.$ext";
  my @cmd = (
    'ffmpeg',  '-y',        '-i',       $raw,
    '-acodec', 'libfaac',   '-ab',      BIT_RATE,
    '-ar',     SAMPLE_RATE, '-threads', '0',
    $tmp
  );
  say join ' ', @cmd;
  if ( system @cmd ) {
    say "Conversion failed: $?";
    return;
  }
  unless ( rename $tmp, $m4a ) {
    say "Can't rename $tmp as $m4a";
    unlink $tmp;
  }
}

# vim:ts=2:sw=2:sts=2:et:ft=perl

