#!/usr/bin/env perl

use 5.010;

use strict;
use warnings;

use File::Find;
use IPC::Run qw( run );

use constant {
  SAMPLE_RATE => 44100,
  BIT_RATE    => '256k',
  EXTENSION   => 'm4a',
};

@ARGV = '.' unless @ARGV;

find {
  no_chdir => 1,
  wanted   => sub {
    return unless /\.(?:flac|wav|wma)(?:\.gz)?$/i;
    toaac( $_ );
  },
 },
 @ARGV;

sub toaac {
  my $raw = shift;
  my $ext = EXTENSION;
  ( my $m4a = $raw ) =~ s/\.(\w+)$/.$ext/;
  if ( -f $m4a ) {
    say "Skipping $raw, $ext file already exists";
    return;
  }
  my $tmp  = "$m4a.tmp.$ext";
  my @pipe = ();
  my $ff   = $raw;

  if ( $raw =~ /\.gz$/i ) {
    push @pipe, [ 'gzip', '-cd', $raw ], '|';
    $ff = '-';
  }

  push @pipe,
   [
    'ffmpeg',  '-y',        '-i',       $ff,
    '-acodec', 'libfaac',   '-ab',      BIT_RATE,
    '-ar',     SAMPLE_RATE, '-threads', '0',
    $tmp
   ],
   '2>', \( my $ffmpeg_err ), '|';

  unless ( run @pipe ) {
    say "Conversion failed: $?\nffmpeg output:\n$ffmpeg_err";
    return;
  }
  unless ( rename $tmp, $m4a ) {
    say "Can't rename $tmp as $m4a";
    unlink $tmp;
  }
}

# vim:ts=2:sw=2:sts=2:et:ft=perl

