#!/usr/bin/env perl

use strict;
use warnings;
use Getopt::Long;
use File::Path;
use File::Basename;

my $name_format = 'msg%06d.eml';
my $sequence    = 1;
my $verbose     = 0;
my $from        = 0;

GetOptions(
  'name=s'  => \$name_format,
  'seq=i'   => \$sequence,
  'from'    => \$from,
  'verbose' => \$verbose,
) or help();

my $fh = undef;
while ( defined( my $line = <> ) ) {
  if ( $line =~ /^From\s/ ) {
    undef $fh;
    next unless $from;
  }
  unless ( defined $fh ) {
    my $name = sprintf( $name_format, $sequence++ );
    my $dir = dirname( $name );
    mkpath( $dir ) unless -d $dir;
    open $fh, '>', $name or die "Can't write $name ($!)\n";
    print "$name\n" if $verbose;
  }
  print $fh $line;
}

sub help {
  print "syntax: mboxchop [options] < mboxfile\n\n"
    . "Options:\n"
    . "  --name     printf style format string for generated names\n"
    . "  --seq      sequence number for generated names\n"
    . "  --from     include the ^From line in output\n"
    . "  --verbose  be verbose\n";
  exit;
}

# vim:ts=2:sw=2:et:ft=perl
