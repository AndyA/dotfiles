#!/usr/bin/env perl

use strict;
use warnings;

use JSON;
use Path::Class;
use User::Utmp qw( :constants :utmpx );

use constant STORE => glob '~/.wtmp.json';

my @WTMP = sort glob '/var/log/wtmp*';
my @KEY  = qw( ut_line ut_pid );

my $stash
 = -f STORE ? JSON->new->decode( scalar file(STORE)->slurp ) : {};

for my $wtmp (@WTMP) {
  utmpxname($wtmp);
  my @db = getutx;
  for my $rec (@db) {
    my $skey = join '-', grep defined && length, @{$rec}{@KEY};
    my $tkey = $rec->{ut_tv}{tv_sec} + $rec->{ut_tv}{tv_usec} / 1_000_000;
    $stash->{$skey}{$tkey} = $rec;
  }
}

my $tmp = STORE . '.tmp';
print { file($tmp)->openw } JSON->new->canonical->encode($stash);
rename $tmp, STORE or die "Can't rename $tmp as ", STORE, ": $!\n";

# vim:ts=2:sw=2:sts=2:et:ft=perl

