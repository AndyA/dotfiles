#!/usr/bin/env perl

use strict;
use warnings;

use File::Spec;
use Getopt::Long;
use Path::Class;

use constant ENTRIES => ( '.svn', 'entries' );

my %opt = ( svn => 0 );

GetOptions( 'svn' => \$opt{svn} ) or die;

@ARGV = ( '.' ) unless @ARGV;
for my $dir ( @ARGV ) {
  walk( $dir );
}

sub walk {
  my $dir = shift;

  my $loc = $opt{svn} ? dir( $dir, '.svn' ) : $dir;
  print "$loc\n";

  my $recline = -1;
  my %rec     = ();
  my @label   = qw( name type );

  my $handle_rec = sub {
    if ( $recline >= @label
      && $rec{type} eq 'dir'
      && length( $rec{name} ) ) {
      walk( dir( $dir, $rec{name} ) );
    }
    $recline = 0;
    %rec     = ();
  };

  LINE:
  for my $line ( file( $dir, ENTRIES )->slurp( chomp => 1 ) ) {
    if ( $line =~ /^\cL$/ ) {
      $handle_rec->();
      next LINE;
    }

    $rec{ $label[$recline] } = $line
     if $recline >= 0 && $recline < @label;

    $recline++;
  }

  $handle_rec->();
}

# vim:ts=2:sw=2:sts=2:et:ft=perl

