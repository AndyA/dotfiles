#!/usr/bin/env perl

use v5.10;

use autodie;
use strict;
use warnings;

use File::Find;
use Getopt::Long;
use JSON;
use Path::Class;

@ARGV = ('.') unless @ARGV;

use constant USAGE => <<EOT;
Syntax: $0 [options] <dir> ...

Options:
    -h, --help              Display this text
    -r, --rename            Rename files as "GOPRO-xxxx-yyyy"
    -d, --dirs              Rename files into dirs "GOPRO-xxxx"
    -p, --prefix=<prefix>   Prefix to use instead of "GOPRO"
        --ffmpeg            Output ffmpeg "concat" format file
EOT

my %O = (
  help   => undef,
  rename => undef,
  dirs   => undef,
  prefix => 'GOPRO',
  ffmpeg => undef,
);

GetOptions(
  'h|help'     => \$O{help},
  'r|rename'   => \$O{rename},
  'd|dirs'     => \$O{dirs},
  'p|prefix:s' => \$O{prefix},
  'ffmpeg'     => \$O{ffmpeg},
) or die USAGE;

if ( $O{help} ) {
  say USAGE;
  exit;
}

my %by_dir = ();
find {
  no_chdir => 1,
  wanted   => sub {
    return unless /\b(?:GOPR|GP\d\d)(\d\d\d\d)\.\w+/;
    my ( $seq, $vid ) = ( $1, file $_);
    my $dir = $vid->parent;
    push @{ $by_dir{"$dir"} }, [$seq, $vid];
  }
}, @ARGV;

for my $dir ( sort keys %by_dir ) {
  my @vids = sort { $a->[0] cmp $b->[0] || $a->[1] cmp $b->[1] }
   @{ $by_dir{$dir} };

  if ( $O{rename} || $O{dirs} ) {
    my @rename = ();
    my $next   = 1;
    for my $vid (@vids) {
      my ($ext) = $vid->[1] =~ /\.([^.]+)$/;
      my $nice = nice_name( $vid->[0] );
      my @new_name = ( sprintf "%s-%04d.%s", $nice, $next++, $ext );
      unshift @new_name, $nice if $O{dirs};

      my $new_vid = file( $dir, @new_name );
      say "$vid->[1] --> $new_vid";
      $new_vid->parent->mkpath;
      rename "$vid->[1]", "$new_vid";
    }
  }
  elsif ( $O{ffmpeg} ) {
    say "file '$_->[1]'" for @vids;
  }
  else {
    say $_->[1] for @vids;
  }
}

sub nice_name {
  my $seq = shift;
  return join '-', $O{prefix}, $seq;
}

# vim:ts=2:sw=2:sts=2:et:ft=perl
