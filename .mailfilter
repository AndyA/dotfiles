#!/usr/bin/perl

use strict;
use warnings;
use Email::Simple;
use Email::Filter;
use Email::Send;
use Mail::ListDetector;
use Mail::SpamAssassin;

my %FROM2LIST = ( 'forum@cheekytrails.co.uk' => 'CheekyForum' );

my $mail = Email::Filter->new;
if ( $mail->from =~ /^MAILER-DAEMON\@/ ) {
    $mail->ignore;
}

spam_check( $mail );
list_check( $mail );

LIST: while ( my ( $from, $list ) = each %FROM2LIST ) {
    if ( $mail->from =~ /\Q$from/ ) {
        set_list( $mail, $list );
    }
}

archive( $mail );

$mail->accept;

sub spam_check {
    my $mail   = shift;
    my $sa     = Mail::SpamAssassin->new;
    my $status = $sa->check( $sa->parse( $mail->simple->as_string ) );
    $mail->simple( Email::Simple->new( $status->rewrite_mail ) );
    if ( $status->is_spam ) {
        $mail->accept( '~/spam' );
    }
}

sub set_list {
    my ( $mail, $list ) = @_;
    $mail->simple->header_set( 'X-Identified-List', $list );
    $mail->simple->header_set( 'X-Is-List',         'YES' );
}

sub list_check {
    my $mail = shift;
    if ( my $list = Mail::ListDetector->new( $mail->simple ) ) {
        set_list( $mail, $list->listname );
    }
}

sub clone_simple { Email::Simple->new( shift->as_string ) }

sub forward {
    my ( $mail, $to ) = @_;
    my $fwd = clone_simple( $mail->simple );
    $fwd->header_set( 'X-Original-To', $fwd->header( 'To' ) );
    $fwd->header_set( 'To',            $to );
    $fwd->header_set( 'Cc' );

    my $sender = Email::Send->new( { mailer => 'SMTP' } );
    $sender->mailer_args( [ Host => 'post.hexten.net' ] );
    $sender->send( $fwd );
}

sub archive {
    my $mail = shift;
    my $archive_to
      = $mail->header( 'X-Identified-List' )
      ? 'hextenlists@gmail.com'
      : 'hexten@gmail.com';
    forward( $mail, $archive_to );
}
